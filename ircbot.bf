[
assumes the nick and channel are in memory, like this:
0 nick 0 channel 0
                 ^ pointer
]

"NICK " ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++.-----.------.++++++++.-------------------------------------------.[-]
nick    <[<]<[<]>[.>]
crlf    +++++++++++++.---.[-]

"USER " +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++.--.--------------.+++++++++++++.--------------------------------------------------.[-]
nick    <[<]>[.>]
" 0 * " ++++++++++++++++++++++++++++++++.++++++++++++++++.----------------.++++++++++.----------.[-]
nick    <[<]>[.>]
crlf    +++++++++++++.---.[-]

"JOIN " ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++.+++++.------.+++++.----------------------------------------------.[-]
channel >[.>]
crlf    +++++++++++++.---.[-]


+[- inf loop


,----------------------------------------------------------[[->+<]>>-<<]
>++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>+

0 ch isc_
[
    [,--------------------------------]
    <,>
]

<[-<+>]<

ch_ 0 0

p --------------------------------------------------------------------------------[[-]>+<]>[-<->]<+[-
    >,[->+>+<<]>>
    0 f ch ch_
    i -------------------------------------------------------------------------[[-]>+<]>[-<->]<+[-
        n ,------------------------------------------------------------------------------[[-]>+<]>[-<->]<+[-
            g ,-----------------------------------------------------------------------[[-]>+<]>[-<->]<+[-
                sp ,--------------------------------[[-]>+<]>[-<->]<+[-
                    set flag <<+>>
                    read ping until \r
                    >,-------------[+++++++++++++>,-------------]
                    skip \n ,[-]
                    "PONG "     ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++.-.-.-------.---------------------------------------.[-]
                    print token <[<]>[.>]
                    crlf        +++++++++++++.---.[-]
                    clear token <[[-]<]
                ]
            ]
        ]
    ]
    <
    channel | 0 f ch_ 0
    r ----------------------------------------------------------------------------------[[-]>+<]>[-<->]<+[-
        i ,-------------------------------------------------------------------------[[-]>+<]>[-<->]<+[-
            v ,--------------------------------------------------------------------------------------[[-]>+<]>[-<->]<+[-
                m ,-----------------------------------------------------------------------------[[-]>+<]>[-<->]<+[-
                    s ,-----------------------------------------------------------------------------------[[-]>+<]>[-<->]<+[-
                        g ,-----------------------------------------------------------------------[[-]>+<]>[-<->]<+[-
                            sp ,--------------------------------[[-]>+<]>[-<->]<+[-
                                set flag <+>
                                channel | 0 flag 0_ 0
                                skip channel +[,--------------------------------]
                                TODO maybe make it check if its a dm
                                skip ":" ,[-]
                                +>
                                0 f1 f2 0_
                                ! ,---------------------------------[[-]>+<]>[-<->]<+[-
                                    a ,-------------------------------------------------------------------------------------------------[[-]>+<]>[-<->]<+[-
                                        d ,----------------------------------------------------------------------------------------------------[[-]>+<]>[-<->]<+[-
                                            d ,----------------------------------------------------------------------------------------------------[[-]>+<]>[-<->]<+[-
                                                sp ,--------------------------------[[-]>+<]>[-<->]<+[-
                                                    <->

                                                    0 f 0 0_

                                                    +[
                                                        >
                                                        1 acc 0
                                                        >
                                                        ,[->+>+<<]
                                                        1 acc 0 c c
                                                        >
                                                        -------------------------------------------
                                                        [[-]
                                                            1 acc 0 0_ c
                                                            <<[->++<]>[-<+++++>]
                                                            >>------------------------------------------------[-<<<+>>>]
                                                            <
                                                        <->]
                                                        1 acc sp 0 c
                                                        <
                                                        +[-
                                                            >>[-]<<
                                                            <<->>
                                                        ]
                                                        <<
                                                    ]
                                                    >>
                                                    +[
                                                        >
                                                        1 acc 0
                                                        >
                                                        ,[->+>+<<]
                                                        1 acc 0 c c
                                                        >
                                                        -------------
                                                        [[-]
                                                            1 acc 0 0_ c
                                                            <<[->++<]>[-<+++++>]
                                                            >>------------------------------------------------[-<<<+>>>]
                                                            <
                                                        <->]
                                                        1 acc sp 0 c
                                                        <
                                                        +[-
                                                            >>[-]<<
                                                            <<->>
                                                        ]
                                                        <<
                                                    ]
                                                    ,[-]
                                                    >[-<<+>>]<<
                                                    0 f 0 0 res_ 0
                                                    [->+<]
                                                    "PRIVMSG " ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++.++.---------.+++++++++++++.---------.++++++.------------.---------------------------------------.[-]
                                                    print channel <<<<<[<]>[.>]>>>>
                                                    " :" ++++++++++++++++++++++++++++++++.++++++++++++++++++++++++++.[-]
                                                    >
                                                    0 f 0 0 0 res_
                                                    [[-<+>]<<->>]
                                                    <[->+<]
                                                    <+[+++++++++++++++++++++++++++++++++++++++++++++++.[-]]
                                                    >>
                                                    [
                                                        <+[
                                                            s_ res 0 0 0 div
                                                            >>++++++++++[
                                                                -
                                                                <
                                                                [->>+>+<<<]
                                                                s 0_ i v v
                                                                >>-[-<<+>>]>
                                                                [[-]<<<<+>>>>]<<<<->>
                                                            ]
                                                            >>>+<<<<<
                                                        ]
                                                        0_ rem 0 0 0 div
                                                        ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>[-<+>]
                                                        >>>>[-<<<+>>>]<<<
                                                        ch 0 div_
                                                        -
                                                    ]
                                                    <<[.[-]<]
                                                    0_ 0 0
                                                    crlf +++++++++++++.---.[-]
                                                ]
                                            ]
                                        ]
                                    ]
                                ]

                                <[-
                                    if second flag stayed clear first flag
                                    [+[,-------------]
                                    ,[-]]
                                    <-.>
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
    <
    <
]
>
-[+
    +[,----------]
]
<

+]
